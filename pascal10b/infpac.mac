;    +--------------------------------------------------------------+
;    |                                                              |
;    |                        S E G I N F O                         |
;    |                        - - - - - - -                         |
;    |                                                              |
;    +--------------------------------------------------------------+
;    
;    MDSI, Company Confidential
;    
;    SYSTEM:  INFPAC - environmental information routines
;    
;    STARTED:  10/13/78
;    
;    PURPOSE:  GET INFORMATION ABOUT USER'S MEMORY SEGMENTS.
;    
;    USAGE:
;    
;       EXTERNAL PROCEDURE SEGINFO(VAR SEGSTUFF: SEGRECD);
;    
;    OUTPUT:
;    
;       SEGSTUFF.LOWLEN
;                   LENGTH OF USER'S LOW SEGMENT IN WORDS.
;    
;       SEGSTUFF.HIGHLEN
;                   LENGTH OF USER'S HIGH SEGMENT IN WORDS  (ZERO  IF
;                   NO HIGH SEGMENT).
;    
;       SEGSTUFF.RDONLY
;                   SET  TO  TRUE  IF  A  HIGH  SEGMENT EXISTS AND IS
;                   READ-ONLY; SET TO FALSE OTHERWISE.
;    
;       SEGSTUFF.SHARABLE
;                   SET TO TRUE IF  A  HIGH  SEGMENT  EXISTS  AND  IS
;                   SHARABLE; SET TO FALSE OTHERWISE.
;    
;    INCLUDE FILES REQUIRED:
;    
;       INFPAC.INC
;    
;    RESPONSIBLE:  S.  M.  ROUSH
;    
;    ----------------------------------------------------------------
        TITLE	SEGINF - GET MEMORY SEGMENT INFORMATION.
	TWOSEG
	RELOC	400000

;  DECLARE ENTRY POINT.

	ENTRY SEGINF


; SEARCH FOR MONITOR CALL RELATED SYMBOLS.

	SEARCH	UUOSYM


;  DEFINE REGISTER SYMBOLICS.

AC2=2
AC10=10
STK=17

;  DEFINE OFFSETS OF FIELDS WITHIN SEGRECD.

LOWLEN=0
HIGHLEN=1
RDONLY=2
SHARED=3

;
;

SEGINF:
	HRRZ	AC10,.JBREL	; GET HIGHEST LEGAL LOW SEG ADDRESS.
	AOS	,AC10		; ADD 1 TO GET LENGTH.
	MOVEM	AC10,LOWLEN(AC2)	; MOVE TO USER RECORD.
;
	HLRZ	AC10,.JBHRL	; GET HIGH SEGMENT LENGTH.
	MOVEM	AC10,HIGHLEN(AC2)	; MOVE TO USER RECORD.
;
	SETZM	,SHARED(AC2)	; INIT BOTH 'SHARED' AND 'RDONLY'
	SETZM	,RDONLY(AC2)	;    TO FALSE.
;
	HRROI	AC10,.GTSGN	; SEGMENT NUMBER TABLE.
	GETTAB	AC10,
	JRST	ROTST		; NO HIGH SEGMENT??
	TDNE	AC10,[SN%SHR]	; SKIP IF NON-SHARABLE.
	AOS	,SHARED(AC2)	; SET 'SHARED' TO TRUE.
;
ROTST:
	HRROI	AC10,.GTSGN	; SEGMENT NUMBER TABLE.
	GETTAB	AC10,
	JRST	GSIEND		; NO HIGH SEGMENT???
	TDNN	AC10,[SN%UWP]	; SKIP IF WRITE ENABLED.
	AOS	,RDONLY(AC2)	; SET 'RDONLY' FIELD TO TRUE.
;
GSIEND:
	POPJ	STK,		; RETURN TO CALLER.
;
;
	PRGEND


;    +--------------------------------------------------------------+
;    |                                                              |
;    |                         U S R S T A                          |
;    |                         - - - - - -                          |
;    |                                                              |
;    +--------------------------------------------------------------+
;    
;    MDSI, Company Confidential
;    
;    STARTED:  10/13/78
;    
;    PURPOSE: 
;    
;    USAGE:
;    
;       EXTERNAL PROCEDURE USRSTAT(VAR USRSTATS: USTATREC);
;    
;    OUTPUT:
;    
;       USRSTATS.CRUS60
;                   'HIGH PRECISION' CRUS USED BY USER'S JOB.  DIVIDE
;                   BY 60 TO GET INTEGRAL NUMBER OF CRUS.
;    
;       USRSTATS.DISKRDS
;                   NUMBER OF DISK BLOCKS READ BY USER JOB.
;    
;       USRSTATS.DISKWRS
;                   NUMBER OF DISK BLOCKS WRITTEN BY USER JOB.
;    
;       USRSTATS.RUNTIME
;                   CURRENT  RUNTIME  OF  USER'S JOB IN 80 NANOSECOND
;                   UNITS.
;    
;       USRSTATS.ELAPTIME
;                   CURRENT TIME  ELAPSED  SINCE  JOB  INITIATION  IN
;                   SECONDS.
;    
;    INCLUDE FILES REQUIRED:
;    
;       INFPAC.INC
;    
;    RESPONSIBLE:  S.  M.  ROUSH
;    
;    ----------------------------------------------------------------
	TITLE	USRSTA - GET USER JOB STATISTICS.
	TWOSEG
	RELOC	400000

;  DECLARE ENTRY POINT.

	ENTRY	USRSTA



;  SEARCH FOR MONITOR CALL RELATED SYMBOLS.

	SEARCH	UUOSYM



;  DEFINE REGISTER SYMBOLICS.

AC2=2
AC10=10
AC11=11
AC12=12
STK=17


;  DEFINE OFFSETS WITHIN USTATREC.

CRUS=0
DRDS=1
DWRTS=2
RUNT=3
ELAPT=4


;

USRSTA:
	MOVE	AC10,[XWD 2,ACBLK]
	CALLI	AC10,-24	; GET *LOW*-PRECISION CRU'S
	SETZM	ACBLK+1		; ERROR RETURN, SAY ZERO
	MOVE	AC10,ACBLK+1	; GET ANSWER
	IMULI	AC10,^D60	; * 60 TO FAKE HIGH PRECISION
	MOVEM	AC10,CRUS(AC2)	; MOVE TO USER'S RECORD
;
	HRROI	AC10,.GTRCT	; DISK READS COUNT TABLE.
	GETTAB	AC10,
	JFCL			; IGNORE ERROR RETURNS.
	TLZ	AC10,777700	; stomp high order crap
	MOVEM	AC10,DRDS(AC2)	; MOVE TO USER RECORD.
;
	HRROI	AC10,.GTWCT	; DISK WRITES COUNT TABLE.
	GETTAB	AC10,
	JFCL			; IGNORE ERROR RETURNS.
	TLZ	AC10,777700	; stomp high order crap
	MOVEM	AC10,DWRTS(AC2)	; MOVE TO USER RECORD.
;
	MOVEI	AC10,1(STK)	; LOCATION FOR RETURN VALUES.
	HRLI	AC10,4
	CALLI	AC10,-57	; READS E-BOX AND M-BOX TIMERS.
	JFCL
	DMOVE	AC10,1(STK)	; GET E-BOX.
	ASHC	AC10,^D23	; NORMALIZE.
	MOVEM	AC10,RUNT(AC2)	; MOVE TO USER RECORD.
;
	HRROI	AC10,.GTJLT	; TIME JOB WAS LOGGED IN ( IN
	GETTAB	AC10,		;    UNIVERSAL DAY/TIME FORMAT.
	JFCL			; IGNORE ERROR RETURN.

	MOVE	AC11,[%CNDTM]	; CURRENT UNIVERSAL
	GETTAB	AC11,		;    DAY/TIME.
	JFCL			; IGNORE ERROR RETURN.

	SUB	AC11,AC10	; ELAPSED UNIVERSAL TIME.
	MULI	AC11,^D86400	; MULTIPLY TIMES SECS PER DAY.
	HLRZM	AC12,ELAPT(AC2)	; MOVE TO USER RECORD.
;
	POPJ	STK,		; RETURN TO CALLER.
;
;
	RELOC	0		; ACUUO. BLOCK IN LOWSEG
ACBLK:	EXP	-1		; GET DATA FOR THIS JOB
	EXP	0		; CRU'S COUNT GOES HERE
;
	PRGEND

;    +--------------------------------------------------------------+
;    |                                                              |
;    |                         J O B I N F                          |
;    |                         - - - - - -                          |
;    |                                                              |
;    +--------------------------------------------------------------+
;    
;    MDSI, Company Confidential
;    
;    STARTED:  10/20/78
;    
;    PURPOSE: 
;    
;    USAGE:
;    
;       EXTERNAL PROCEDURE JOBINFO(VAR JOBSTUFF: JOBREC);
;    
;    OUTPUT:
;    
;       JOBSTUFF.HSEGPPN
;                   PPN ASSOCIATED WITH JOB'S HIGH SEGMENT.
;    
;       JOBSTUFF.HSEGNAM
;                   NAME OF JOB'S HIGH SEGMENT.
;    
;       JOBSTUFF.LSEGPPN
;                   PPN ASSOCIATED WITH JOB'S LOW SEGMENT.
;    
;       JOBSTUFF.LSEGNAM
;                   NAME ASSOCIATED WITH JOB'S LOW SEGMENT.
;    
;       JOBSTUFF.PROGDIR
;                   PPN PROGRAM WAS RUN FROM.
;    
;       JOBSTUFF.JOBNUM
;                   NUMBER ASSIGNED THIS JOB AT LOGIN.
;    
;    NOTES:  1.  A  NULL  STRING  WILL  BE  RETURNED  IF  ERRORS  ARE
;       DETECTED  (FOR EXAMPLE, NO HIGH SEGMENT).  2.  IF THE PROGRAM
;       IS RUN UNDER LINK WITHOUT A /SAVE OR /SSAVE, THE HIGH SEGMENT
;       PPN IS RETURNED AS [0,0].
;    
;    INCLUDE FILES REQUIRED:
;    
;       INFPAC.INC
;    
;    RESPONSIBLE:  S.  M.  ROUSH
;    
;    ----------------------------------------------------------------
	TITLE	JOBINF - GET USER JOB INFORMATION.
	TWOSEG
	RELOC	400000

; DECLARE ENTRY POINT

	ENTRY	JOBINF

; SEARCH FOR UUO SYMBOLS.

	SEARCH	UUOSYM

; DEFINE REGISTER SYMBOLICS

AC2=2
AC10=10
AC11=11
AC12=12
AC13=13
AC14=14
STK=17

; DEFINE OFFSETS OF FIELDS WITHIN A JOBREC.

HSEGPPN=0
HSEGNAM=4
LSEGPPN=7
LSEGNAM=^D11
PROGDIR=^D14
JOBNUM=^D18
PROJID=^D19

opdef	ADJBP[IBP]


JOBINF:
	SETZM	HSEGPPN(AC2)		; INIT STRING LENGTH TO 0.
	MOVE	AC10,[-2,,.GTPPN]
	GETTAB	AC10,			; GET HIGH SEG PPN.
	JRST	HNAM			; ERROR - RETURN NULL STRING.
	MOVEI	AC11,HSEGPPN(AC2)	; AC11 - ADDRESS OF RESULT STRING.
	PUSHJ	STK,WRTPPN		; CONVERT PPN TO STRING.

;

HNAM:
	SETZM	HSEGNAM(AC2)		; INIT STRING LENGTH TO ZERO.
	MOVE	AC10,[-2,,.GTPRG]	;
	GETTAB	AC10,			; GET HIGH SEG NAME.
	JRST	LPPN			; ERROR - NULL STRING RETURNED.
	MOVEI	AC11,HSEGNAM(AC2)	; AC11 - ADDR OF DEST STRING.
	PUSHJ	STK,WRTNAM		; CONVERT NAME TO STRING.

;

LPPN:
	SETZM	LSEGPPN(AC2)		; INIT STRING LENGTH TO ZERO.
	HRROI	AC10,.GTPPN
	GETTAB	AC10,			; GET LOW SEG PPN.
	JRST	LNAM			; ERROR - RETURN NULL STRING.
	MOVEI	AC11,LSEGPPN(AC2)	; AC11 - ADDR OF STRING RESULT.
	PUSHJ	STK,WRTPPN		; CONVERT PPN TO STRING.

;

LNAM:
	SETZM	LSEGNAM(AC2)		; INIT STRING LENGTH TO ZERO.
	HRROI	AC10,.GTPRG		; 
	GETTAB	AC10,			; GET LOW SEG NAME.
	JRST	PDIR			; ERROR - NULL STRING RETURNED.
	MOVEI	AC11,LSEGNAM(AC2)	; AC11 - ADDR OF RESULT STRING.
	PUSHJ	STK,WRTNAM		; CONVERT NAME TO STRING.

;

PDIR:
	SETZM	PROGDIR(AC2)		; INIT STRING LENGTH TO ZERO.
	HRROI	AC10,.GTRDI		;
	GETTAB	AC10,			; GET DIRECTORY PROGRAM WAS RUN FROM.
	JRST	JOBNO			; ERROR - RETURN NULL STRING.
	MOVEI	AC11,PROGDIR(AC2)	; AC11 - ADDR OF RESULT STRING.
	PUSHJ	STK,WRTPPN		; CONVERT TO STRING.

;

JOBNO:
	PJOB	AC10,			; GET JOB NUMBER.
	MOVEM	AC10,JOBNUM(AC2)	; NO ERROR RETURN.


	MOVEI	AC11,PROJID(AC2)	; addr of project id field (JDR)
	SETZM	0,0(AC11)		; null string
	HRROI	AC10,.GTNM1		; arg for call
	GETTAB	AC10,0			; get first half of name
	POPJ	STK,0
	PUSHJ	STK,WRTNAM		; write the name into the record

	HRROI	AC10,.GTNM2		; second half of name
	GETTAB	AC10,0			; go for it
	JRST	NAMERR			; half a name is no good
	JRST	WRTNAM			; putinto record and split

NAMERR:	SETZM	0,0(AC11)		; null string
	POPJ	STK,0			; split

;

	POPJ	STK,			; RETURN TO CALLER.


;  WRTPPN - CONVERTS THE PPN IN AC10 INTO A STRING
;  ( '[XXXX,YYYY]' FORMAT).  THE ADDRESS OF THE DESTINATION
;  STRING SHOULD BE IN AC11 UPON ENTRY.  THE FIRST WORD OF 
;  THE STRING IS THE LENGTH OF THE RESULT WHICH IS SET BY
;  THIS ROUTINE, AND, ASSUMED TO BE ZERO ON ENTRY.

WRTPPN:
	MOVE	AC12,[POINT 7,1(AC11)]
	MOVEI	AC14,"["		; WRITE LEFT BRACKET.
	PUSHJ	STK,WRCHR
	HLRZ	AC13,AC10		; WRITE PROJECT NUMBER.
	PUSHJ	STK,WRNUM
	MOVEI	AC14,","		; WRITE THE COMMA.
	PUSHJ	STK,WRCHR
	HRRZ	AC13,AC10		; WRITE PROGRAMMER NUMBER.
	PUSHJ	STK,WRNUM
	MOVEI	AC14,"]"		; WRITE RIGHT BRACKET.
	PUSHJ	STK,WRCHR		
	POPJ	STK,			; RETURN TO CALLER.

;  WRTNAM - CONVERT SIXBIT CONTENTS OF AC10 TO ASCII AND
;  MOVE TO PASCAL STRING ADDRESSED BY AC11.  INCREMENTS
;  LENGTH FIELD OF STRING BY 6.

WRTNAM:
	MOVE	AC12,[POINT 7,1(AC11)]
	MOVE	AC13,0(AC11)		; get current length (JDR)
	JUMPE	AC13,LOOP1-1		; skip ADJPB if zero length
	ADJBP	AC13,AC12
	MOVE	AC12,AC13
	MOVEI	AC13,6
;
LOOP1:
	ROT	AC10,6			; ROTATE RIGHT 6 BITS.
	MOVE	AC14,AC10		; ISOLATE LOWER 6 BITS.
	ANDI	AC14,77			; 
	ADDI	AC14,40			; CONVERT SIXBIT TO ASCII.
	PUSHJ	STK,WRCHR		; MOVE IT, INCR B.P., INCR STRING LEN.
	SOJN	AC13,LOOP1		; DO IT HALF A DOZEN TIMES!
	
	POPJ	STK,			; RETURN.

;  WRCHR - MOVE CHARACTER IN AC14 TO DESTINATION VIA
;  BYTE POINTER IN AC12.  INCREMENT BYTE POINTER AND 
;  STRING LENGTH FIELD.  STRING LENGTH FIELD ADDRESS IS
;  IN AC11.

WRCHR:
	AOS	(AC11)			; INCREMENT STRING LENGTH.
	IDPB	AC14,AC12		; MOVE BYTE, INCR B.P.
	POPJ	STK,			; RETURN.

; WRNUM - CONVERT INTEGER IN AC13 TO ASCII OCTAL STRING.
;  DESTINATION BYTE POINTER IS IN AC12.  INCREMENT BYTE POINTER 
;  AND STRING LENGTH FIELD.  STRING LENGTH FIELD ADDRESS IS IN AC11.

WRNUM:
	MOVE	AC14,AC13		; ISOLATE LOW ORDER DIGIT.
	ANDI	AC14,7
	ADDI	AC14,"0"		; CONVERT TO ASCII.
	PUSH	STK,AC14		; SAVE IT.
	LSH	AC13,-3			; GET REMAINING NUMBER.
	SKIPE	AC13			; IF NOT ZERO, THEN
	PUSHJ	STK,WRNUM		;   RECURSIVELY CONVERT IT.
	POP	STK,AC14		; GET LEFTMOST REMAINING ASCII
	PUSHJ	STK,WRCHR		;   DIGIT AND WRITE IT.
	POPJ	STK,			; RETURN TO CALLER, WHOMEVER IT
					;   HAPPENS TO BE THIS TIME AROUND.
;
;
	PRGEND


;    +--------------------------------------------------------------+
;    |                                                              |
;    |                         S Y S S T A                          |
;    |                         - - - - - -                          |
;    |                                                              |
;    +--------------------------------------------------------------+
;    
;    MDSI, Company Confidential
;    
;    SYSTEM:  INFPAC - environmental information routines
;    
;    STARTED:  10/13/78
;    
;    PURPOSE: 
;    
;    USAGE:
;    
;       EXTERNAL PROCEDURE SYSSTAT(VAR SYSSTATS: SSTATREC);
;    
;    OUTPUT:
;    
;       SYSSTATS.NLOGIN
;                   NUMBER OF JOBS CURRENTLY LOGGED IN.
;    
;       SYSSTATS.MAXCOR
;                   SIZE IN WORDS OF LARGEST LEGAL USER JOB.
;    
;    INCLUDE FILES REQUIRED:
;    
;       INFPAC.INC
;    
;    RESPONSIBLE:  S.  M.  ROUSH
;    
;    ----------------------------------------------------------------
	TITLE	SYSSTA - GET SYSTEM STATISTICS.
	TWOSEG
	RELOC	400000

;  DECLARE ENTRY POINT.

	ENTRY	SYSSTA

;  SEARCH FOR MONITOR CALL EXTERNALS.

	SEARCH	UUOSYM

;  DEFINE REGISTER SYMBOLICS.

AC2=2
AC10=10
AC11=11
STK=17

; DEFINE OFFSETS OF FIELDS WITHIN A SSTATREC.


NLOGIN=0
MAXCOR=1


SYSSTA:
	MOVE	AC10,[%CNLNM]
	GETTAB	AC10,		; GET NUMBER OF JOBS LOGGED IN.
	JFCL			; IGNORE ERROR RETURN.
	MOVEM	AC10,NLOGIN(AC2)	; MOVE TO USER RECORD.
;

	MOVE	AC10,[%NSCMX]
	GETTAB	AC10,		; CORMAX.
	JFCL			; IGNORE ERROR RETURN.
	MOVEM	AC10,MAXCOR(AC2)	; MOVE TO USER LOC.
;
	POPJ	STK,
;
;
	END


