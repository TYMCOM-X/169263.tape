	TITLE	ONESCAPE   *** PSUEDO ESCAPE HANDLER ***
	TWOSEG

	SEARCH	PASSYM


;
;     .JBINT - MONITOR LOOKS HERE WHEN A CNTRL-C IS ISSUED,
;	IF NONZERO, IT IS INTERPRETED AS A POINTER TO AN
;	INTERRUPT CONTROL BLOCK 
;
	LOC	134
	EXP	INTBLK


;
;     INTERRUPT CONTROL BLOCK - SET UP TO TRAP CONTROL-C'S. IT IS
;	INITIALIZED SO THAT, ISSUING A CONTROL-C BEFORE THIS HAS 
;	BEEN CALLED WILL CAUSE A RETURN TO THE MONITOR.  THIS IS DONE
;       BY MAKING THE 'LAST PC' NONZERO SO THAT MONITOR THINKS IT IS
;       GETTING A RECURSIVE INTERRUPT.
;

	RELOC	0
INTBLK:	XWD	3,ESCINT	; 3 WORDS LONG, PLACE TO START
	XWD	0,2		; NO MESSAGE, TYPE 2 - CNTRL-C
	EXP	1		; LAST PC, NONZERO TO CAUSE EXIT BEFORE INIT
	EXP	0		; GET INT CLASS

INTLOC:	EXP	0		; PLACE TO GO IN USER CODE
INTDSP:	EXP	0		; BASIS OF FRAME ASSOCIATED WITH ABOVE
ESCCNT:	EXP	0		; COUNT OF ESCAPES WHILE MASKED


;
;     ONESCAPE - PASCAL CALLABLE FUNCTION RETURNING A BOOLEAN VALUE.
;	WHEN CALLED IT ENABLES INTERRUPTS AND RETURNS FALSE.  LATER
;       WHEN A CONTROL-C IS ISSUED, IT RETURNS TO THE SAME PLACE WITH
;       THE VALUE TRUE.  THUS ONE CAN CONSTRUCT A HANDLER IN THE
;       FOLLOWING MANNER:
;
;	     IF ONESCAPE THEN action ;
;

	RELOC	400000
	ENTRY	ONESCA
	ENTRY	MASK
	ENTRY	UNMASK
ONESCA:	SETZM	FV(TOPP)	; RETURN FALSE ON SETUP CALL
	HRRZM	BASIS,INTDSP	; SAVE STACK FRAME WHICH MADE CALL
	MOVE	0,0(TOPP)	; SAVE RETURN ADDRESS
	MOVEM	0,INTLOC
	SETZM	INTBLK+2	; ENABLE INTERRUPT
	POPJ	TOPP,


;
;     CONTROL-C HANDLER:  WHEN ISSUED COME HERE AND FAKE A RETURN INTO
;	THE RIGHT FRAME.
;

	EXTERN	UNWND.

ESCINT:	MOVE	1,INTDSP	; UNWND. TO FRAME OF HANDLER
	CAIE	1,0(BASIS)	; IF NOT ALREADY THERE
	PUSHJ	TOPP,UNWND.
	MOVE	1,INTLOC	; FAKE CALL
	PUSH	TOPP,1		;   PLACE RETURN ADDRESS ON STACK
	MOVEI	1,1		; RETURN TRUE
	MOVEM	1,FV(TOPP)
	SETZM	INTBLK+2	; REENABLE INTERRUPTS
	POPJ	TOPP,


;
;     MASK AND UNMASK - SUPPRESSES ESCAPE INTERRUPTS
;

MASK::	SETZM	ESCCNT		; COUNT OF ESCAPES DURING TIME MASKED
	MOVEI	AC0,MSKESC	; SETUP HANDLER FOR MASKED ESC'S
	HRRM	AC0,INTBLK
	POPJ	TOPP,

MSKESC:	AOS	ESCCNT		; RECORD ESCAPE INTERRUPT
	PUSH	TOPP,INTBLK+2	; SAVE RETURN ADDRESS
	SETZM	INTBLK+2	; ENABLE NEXT INTERRUPT
	POPJ	TOPP,

UNMASK::MOVEI	AC0,ESCINT	; ENABLE STD HANDLER
	HRRM	AC0,INTBLK
	SKIPE	ESCCNT
	JRST	ESCINT		; IF INTERRUPTS OCCURED WHILE MASKED, SIGNAL
	POPJ	TOPP,

;
;     ESCOFF - TURNS OFF CONTROL-C HANDLER
;

	ENTRY	ESCOFF
ESCOFF:	MOVEI	AC0,1		; SET LAST PC NON-ZERO TO CAUSE EXIT 1
	MOVEM	AC0,INTBLK+2
	POPJ	TOPP,
	END
