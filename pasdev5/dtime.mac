;    +--------------------------------------------------------------+
;    |                                                              |
;    |                        D A Y T I M E                         |
;    |                        - - - - - - -                         |
;    |                                                              |
;    +--------------------------------------------------------------+
;    
;    MDSI, Company Confidential
;    
;    SYSTEM:  DTIME - day/time manipulation routines
;    
;    STARTED:  11/01/78
;    
;    PURPOSE:  Return current local day/time in internal form.
;    
;    USAGE:
;    
;       EXTERNAL FUNCTION DAYTIME: DTIME_INT;
;    
;    OUTPUT:
;    
;       <return value>
;                   the  current  local  day/time  is   returned   in
;                   internal form.
;    
;    INCLUDE FILES REQUIRED:
;    
;       DTIME.TYP
;       DTIME.INC
;    
;    RESPONSIBLE:  S.  M.  ROUSH
;    
;    ----------------------------------------------------------------
        TITLE	DAYTIME
	TWOSEG
	RELOC	400000

	ENTRY	DAYTIM
	SEARCH	PASSYM(RND:PASSYM.UNV)

AC10=10
AC11=11

; UUO PARAMETER BLOCK.

PARAMS:
	EXP	1			; NUMBER OF FUNCTIONS.
	XWD	6,AC11			; FCN 6 - LOCAL UNIV DAY/TIME.


;  THE CODE !

DAYTIM:
;
; GET DATE AND TIME AND CONVERT THEM TO INTERNAL FORM.
;
	DATE	2,
	PUSHJ	TOPP,EC%DCDATE##
	PUSH	TOPP,FV+1(TOPP)
	MSTIME	2,
	PUSHJ	TOPP,EC%DCTIME##
	POP	TOPP,1
	HRR	2,1
	MOVEM	2,FV(TOPP)
	POPJ	TOPP,
;
;
	SETZM	4(TOPP)		; ZERO RETURN VALUE IN CASE OF ERRORS.

	HRROI	AC10,^o40	; gettab 40 (batch status) for current job
	GETTAB	AC10,0		; do it
	POPJ	TOPP,0		; error, return zero (false?)
	TLZ	AC10,777577	; clear all but batch bit
	HRRI	AC10,PARAMS	; load param addr
	TLZN	AC10,200	; is the batch bit set?
	HRLI	AC10,777777	; no, set left half to minus one (this job)
	CALLI	AC10,-51		;   INFO.
	POPJ	TOPP,			; ERROR - RETURN 0.
	MOVEM	AC11,4(TOPP)		; MOVE DATE TO RETURN LOC.
;
	POPJ	TOPP,			; RETURN TO CALLER.
;
;
	PRGEND


;    +--------------------------------------------------------------+
;    |                                                              |
;    |                      E X T R _ D A T E                       |
;    |                      - - - - - - - - -                       |
;    |                                                              |
;    +--------------------------------------------------------------+
;    
;    MDSI, Company Confidential
;    
;    SYSTEM:  DTIME - day/time manipulation routines
;    
;    STARTED:  11/01/78
;    
;    PURPOSE:  Extract the date portion of an internal day/time.
;    
;    USAGE:
;    
;       EXTERNAL FUNCTION EXTR_DATE(DTIME: DTIME_INT): DATE_INT;
;    
;    INPUT:
;    
;       DTIME       a day/time in internal form.
;    
;    OUTPUT:
;    
;       <return value>
;                   the date portion of 'DTIME' in internal form.
;    
;    INCLUDE FILES REQUIRED:
;    
;       DTIME.TYP
;       DTIME.INC
;    
;    RESPONSIBLE:  S.  M.  ROUSH
;    
;    ----------------------------------------------------------------
	TITLE	EXTR_DATE
	TWOSEG
	RELOC	400000

	ENTRY	EXTR%D
	SEARCH	PASSYM(RND:PASSYM.UNV)

AC10=10
AC11=11

EXTR%D:
	HLLZM	REG,4(TOPP)		; RETURN DATE,,0.
;
	POPJ	TOPP,


	PRGEND


;    +--------------------------------------------------------------+
;    |                                                              |
;    |                      E X T R _ T I M E                       |
;    |                      - - - - - - - - -                       |
;    |                                                              |
;    +--------------------------------------------------------------+
;    
;    MDSI, Company Confidential
;    
;    SYSTEM:  DTIME - day/time manipulation routines
;    
;    STARTED:  11/01/78
;    
;    PURPOSE:  Extract the time portion of an internal day/time.
;    
;    USAGE:
;    
;       EXTERNAL FUNCTION EXTR_TIME(DTIME: DTIME_INT): TIME_INT;
;    
;    INPUT:
;    
;       DTIME       a day/time in internal form.
;    
;    OUTPUT:
;    
;       <return value>
;                   the time portion of 'DTIME' in internal form.
;    
;    INCLUDE FILES REQUIRED:
;    
;       DTIME.TYP
;       DTIME.INC
;    
;    RESPONSIBLE:  S.  M.  ROUSH
;    
;    ----------------------------------------------------------------
	TITLE	EXTR_TIME
	TWOSEG
	RELOC	400000

	ENTRY	EXTR%T
	SEARCH	PASSYM(RND:PASSYM.UNV)

AC10=10
AC11=11

EXTR%T:
	HRRZM	REG,4(TOPP)		; RETURN 0,,TIME.
;
	POPJ	TOPP,
;
;
	PRGEND


;    +--------------------------------------------------------------+
;    |                                                              |
;    |                     D T _ C O M B I N E                      |
;    |                     - - - - - - - - - -                      |
;    |                                                              |
;    +--------------------------------------------------------------+
;    
;    MDSI, Company Confidential
;    
;    SYSTEM:  DTIME - day/time manipulation routines
;    
;    STARTED:  11/01/78
;    
;    PURPOSE:   Combine  an  internal  date  with  an  internal  time
;       yielding an internal day/time.
;    
;    USAGE:
;    
;       EXTERNAL FUNCTION DT_COMBINE(DATE: DATE_INT;
;          TIME: TIME_INT): DTIME_INT;
;    
;    INPUT:
;    
;       DATE        a date in internal form.
;    
;       TIME        a time in internal form.
;    
;    OUTPUT:
;    
;       <return value>
;                   the   internal   day/time   resulting   from  the
;                   composition of the two input parameters.
;    
;    INCLUDE FILES REQUIRED:
;    
;       DTIME.TYP
;       DTIME.INC
;    
;    RESPONSIBLE:  S.  M.  ROUSH
;    
;    ----------------------------------------------------------------
	TITLE	DT_COMBINE
	TWOSEG
	RELOC	400000

	ENTRY	DT%COM
	SEARCH	PASSYM(RND:PASSYM.UNV)

AC10=10
AC11=11

DT%COM:
	HLLM	REG,4(TOPP)		; MOVE DATE TO RESULT WORD.
	HRRM	REG1,4(TOPP)		; MOVE TIME TO RESULT WORD.
;
	POPJ	TOPP,
;
;
	PRGEND


;    +--------------------------------------------------------------+
;    |                                                              |
;    |                      D A Y S _ D I F F                       |
;    |                      - - - - - - - - -                       |
;    |                                                              |
;    +--------------------------------------------------------------+
;    
;    MDSI, Company Confidential
;    
;    SYSTEM:  DTIME - day/time manipulation routines
;    
;    STARTED:  11/01/78
;    
;    PURPOSE:  Compute  the  number  of  days  between  two  internal
;       day/times.
;    
;    USAGE:
;    
;       EXTERNAL FUNCTION DAYS_DIFF(DTIME1, DTIME2: DTIME_INT): DAYS;
;    
;    INPUT:
;    
;       DTIME1      a day/time in internal form.
;    
;       DTIME2      a day/time in internal form.
;    
;    OUTPUT:
;    
;       <return value>
;                   whole  number  of days between the two day/times.
;                   This value may be negative; the  second  day/time
;                   is subtracted from the first day/time.
;    
;    INCLUDE FILES REQUIRED:
;    
;       DTIME.TYP
;       DTIME.INC
;    
;    RESPONSIBLE:  S.  M.  ROUSH
;    
;    ----------------------------------------------------------------
	TITLE	DAYS_DIFF
	TWOSEG
	RELOC	400000

	ENTRY	DAYS%D
	SEARCH PASSYM(RND:PASSYM.UNV)

AC10=10
AC11=11

DAYS%D:
	SUB	REG,REG1		; SUBTRACT DAY/TIMES.
	IDIV	REG,[1000000]		; SCALE DOWN RESULT - A SHIFT WILL
					;    NOT WORK!
	MOVEM	REG,4(TOPP)		; MOVE TO FUNCTION RETURN LOCATION.
;
	POPJ	TOPP,
;
;
	PRGEND


;    +--------------------------------------------------------------+
;    |                                                              |
;    |                      S E C S _ D I F F                       |
;    |                      - - - - - - - - -                       |
;    |                                                              |
;    +--------------------------------------------------------------+
;    
;    MDSI, Company Confidential
;    
;    SYSTEM:  DTIME - day/time manipulation routines
;    
;    STARTED:  11/01/78
;    
;    PURPOSE:  Compute the number of  seconds  between  two  internal
;       day/times.
;    
;    USAGE:
;    
;       EXTERNAL FUNCTION SECS_DIFF(DTIME1, DTIME2: DTIME_INT): SECONDS;
;    
;    INPUT:
;    
;       DTIME1      a day/time in internal form.
;    
;       DTIME2      a day/time in internal form.
;    
;    OUTPUT:
;    
;       <return value>
;                   rounded   number   of  seconds  between  the  two
;                   internal day/times.  This value may be  negative;
;                   the  second day/time is subtracted from the first
;                   day/time.
;    
;    INCLUDE FILES REQUIRED:
;    
;       DTIME.TYP
;       DTIME.INC
;    
;    RESPONSIBLE:  S.  M.  ROUSH
;    
;    ----------------------------------------------------------------
	TITLE	SECS_DIFF
	TWOSEG
	RELOC	400000

	ENTRY	SECS%D
	SEARCH	PASSYM(RND:PASSYM.UNV)


SECS%D:
	SUB	REG,REG1		; DAYS DIFFERENCE.
	MULI	REG,^D24 * ^D60 * ^D60	; MULT BY SECONDS PER DAY.

	DIV	REG,[1000000]		; SCALE RESULT - A SHIFT WILL NOT
					;    WORK!

	JUMPL	REG1,REMNEG		; ROUND RESULT.
	
	CAIL	REG1,400000
	AOS	REG
	JRST	MOVANS

REMNEG:	CAMG	REG1,[777777400000]
	SOJ	REG,

MOVANS:
	MOVEM	REG,4(TOPP)		; MOVE TO FUNCTION RETURN LOCATION.
;
	POPJ	TOPP,
;
;
	PRGEND


;    +--------------------------------------------------------------+
;    |                                                              |
;    |                       A D D _ D A Y S                        |
;    |                       - - - - - - - -                        |
;    |                                                              |
;    +--------------------------------------------------------------+
;    
;    MDSI, Company Confidential
;    
;    SYSTEM:  DTIME - day/time manipulation routines
;    
;    STARTED:  11/01/78
;    
;    PURPOSE:  Add a given number of days to an internal day/time.
;    
;    USAGE:
;    
;       EXTERNAL FUNCTION ADD_DAYS(DTIME: DTIME_INT;
;          NUM_DAYS: DAYS): DTIME_INT;
;    
;    INPUT:
;    
;       DTIME       a day/time in internal form.
;    
;       NUM_DAYS    a (possibly negative) number of days.
;    
;    OUTPUT:
;    
;       <return value>
;                   an  internal  day/time  formed  by   adding   the
;                   specified  number  of days to the given day/time.
;                   If a negative  day/time  would  result  then  the
;                   internal day/time for day zero is returned.
;    
;    INCLUDE FILES REQUIRED:
;    
;       DTIME.TYP
;       DTIME.INC
;    
;    RESPONSIBLE:  S.  M.  ROUSH
;    
;    ----------------------------------------------------------------
	TITLE	ADD_DAYS
	TWOSEG
	RELOC	400000

	ENTRY	ADD%DA
	SEARCH	PASSYM(RND:PASSYM.UNV)


ADD%DA:
	HRLZS	REG1			; MOVE DAYS TO DATE HALFWORD.
	ADD	REG,REG1		; ADD DAYS TO DAY/TIME.
	
	SKIPGE	REG			; IF RESULT IS NEGATIVE,
	SETZ	REG,			;    RETURN A ZERO DAY/TIME.
	MOVEM	REG,4(TOPP)		
;
	POPJ	TOPP,


	PRGEND

;    +--------------------------------------------------------------+
;    |                                                              |
;    |                       A D D _ S E C S                        |
;    |                       - - - - - - - -                        |
;    |                                                              |
;    +--------------------------------------------------------------+
;    
;    MDSI, Company Confidential
;    
;    SYSTEM:  DTIME - day/time manipulation routines
;    
;    STARTED:  11/01/78
;    
;    PURPOSE:  Add a given number of seconds to an internal day/time.
;    
;    USAGE:
;    
;       EXTERNAL FUNCTION ADD_SECS(DTIME: DTIME_INT;
;          NUM_SECS: SECONDS): DTIME_INT;
;    
;    INPUT:
;    
;       DTIME       a day/time in internal form.
;    
;       NUM_SECS    a (possibly negative) number of seconds.
;    
;    OUTPUT:
;    
;       <return value>
;                   an  internal  day/time  formed  by   adding   the
;                   specified   number   of   seconds  to  the  given
;                   day/time.  If a negative  day/time  would  result
;                   then  the  internal  day/time  for  day  zero  is
;                   returned.
;    
;    INCLUDE FILES REQUIRED:
;    
;       DTIME.TYP
;       DTIME.INC
;    
;    RESPONSIBLE:  S.  M.  ROUSH
;    
;    ----------------------------------------------------------------
	TITLE	ADD_SECS
	TWOSEG
	RELOC	400000

	ENTRY	ADD%SE
	SEARCH	PASSYM(RND:PASSYM.UNV)


ADD%SE:
	MUL	REG1,[1000000]		; SCALE SECONDS UP SO BINARY POINT
					;    CORRECT.
	DIVI	REG1,^D24 * ^D60 * ^D60	; DIVIDE BY SECS/DAY TO GET
					;    FRACTION OF DAY.

	JUMPL	REG2,REMNEG		; ROUND RESULT OF DIVIDE.
	CAIL	REG2,^D12 * ^D60 * ^D60	; 
	AOS	REG1
	JRST	ADDRES
REMNEG:
	CAMG	REG2,[-^D12 * ^D60 * ^D60]  ; ROUND - NEGATIVE CASE.
	SOJ	REG1,

ADDRES:
	ADD	REG,REG1		; ADD TO DAY/TIME.

	SKIPGE	REG			; IF RESULT NEGATIVE THEN
	SETZ	REG,			;    RETURN ZERO.

	MOVEM	REG,4(TOPP)		; MOVE TO FCN RETURN VALUE LOCATION.

	POPJ	TOPP,


	PRGEND

	
;    +--------------------------------------------------------------+
;    |                                                              |
;    |                        G M D T I M E                         |
;    |                        - - - - - - -                         |
;    |                                                              |
;    +--------------------------------------------------------------+
;    
;    MDSI, Company Confidential
;    
;    SYSTEM:  DTIME - day/time manipulation routines
;    
;    STARTED:  11/01/78
;    
;    PURPOSE:  Return the current Greenwich Mean day/time in internal
;       form.
;    
;    USAGE:
;    
;       EXTERNAL FUNCTION GMDTIME: DTIME_INT;
;    
;    OUTPUT:
;    
;       <return value>
;                   the  current  Greenwich Mean day/time in internal
;                   form.
;    
;    INCLUDE FILES REQUIRED:
;    
;       DTIME.TYP
;       DTIME.INC
;    
;    RESPONSIBLE:  S.  M.  ROUSH
;    
;    ----------------------------------------------------------------
	TITLE	GMDTIME
	TWOSEG
	RELOC	400000

	ENTRY	GMDTIM
	SEARCH	PASSYM(RND:PASSYM.UNV)

AC10=10
AC11=11

; ARGUMENT BLOCK FOR ADP TIME UUO.

BLOCK:
	EXP	1			; 1 FUNCTION DESIRED.
	XWD	6,AC11			; FCN CODE FOR UNIV DAY/TIME.


GMDTIM:
;
; GET DEC DATE AND TIME AND CONVERT TO INTERNAL FORM.
;
	DATE	2,
	PUSHJ	TOPP,EC%DCDATE##
	PUSH	TOPP,FV+1(TOPP)
	MSTIME	2,
	PUSHJ	TOPP,EC%DCTIME##
	POP	TOPP,1
	HRR	2,1
	MOVEM	2,FV(TOPP)
	POPJ	TOPP,
;
;
	SETZM	4(TOPP)		; IN CASE OF ERRORS.
	MOVE	AC10,[-2,,BLOCK]	; -2 SAYS RETURN DAY/TIMES
	CALLI	AC10,-51		;    IN GMT.
	POPJ	TOPP,			; RETURN ZERO IF ERRORS.
	MOVEM	AC11,4(TOPP)		; MOVE GM DAY/TIME TO RETURN LOC.
;
	POPJ	TOPP,
;
;
	END
   