	TITLE	PATHNAME - GIVES FILENAME OF PASCAL FILE
	SEARCH	PASSYM
	ENTRY	FNAME.
	$RELOC

	OPDEF	ADJSP	[105B8]


;
;    PATHNAME - is a PASCAL callable function returning the actual name
;	of a file (or device) whereever it is.  The name returned reflects
;       any directory searching enabled by the user.  The calling sequence
;	is:
;
;		EXTERNAL FUNCTION PATHNAME (VAR F: TEXT): STRING[32]
;


FNAME.:
PATHNA:	MOVE	REG1,AC0		; GET PTR TO RETURN VAL IN USEABLE REG
	MOVEI	REG2,1(REG1)
	HRLI	REG2,440700		; GET BYTE PTR TO STRING TO RETURN
	SETZM	0(REG1)			; INIT RETURN VALUE TO ''

	SKIPN	FILBUF(REG)		; FILE IS CLOSED
	POPJ	TOPP,			; RETURN ''



;
;    Have needed information in file block; so convert
;	to ASCII and put in return string.
;
        SKIPN   REG3,FILDEV(REG)
        JRST    NODEVICE
	PUSHJ	TOPP,WRNAME
	MOVEI	REG4,":"		; FOLLOWED BY COLON
	PUSHJ	TOPP,WRCHR
NODEVICE:
	MOVE	REG3,FILNAM(REG)	; WRITE FILENAME
	PUSHJ	TOPP,WRNAME
	HLLZ	REG3,FILEXT(REG)	; WRITE EXTENSION IF NONNULL
	JUMPE	REG3,WRDIR
	MOVEI	REG4,"."		; DOT BEFORE EXTENSION NAME
	PUSHJ	TOPP,WRCHR
	PUSHJ	TOPP,WRNAME
WRDIR:	MOVEI	REG4,"["		; WRITE [DIRECTORY]
	PUSHJ	TOPP,WRCHR
        HLRZ    REG3,FILPPN(REG)
	PUSHJ	TOPP,WRNUM
	MOVEI	REG4,","
	PUSHJ	TOPP,WRCHR
        HRRZ    REG3,FILPPN(REG)
	PUSHJ	TOPP,WRNUM
	MOVEI	REG4,"]"
			; return through WRCHR routine

;
;    Write routines move text into return string
;

WRCHR:	AOS	0(REG1)			; INCREMENT LENGTH
	IDPB	REG4,REG2
	POPJ	TOPP,

WRNUM:	MOVE	REG4,REG3		; ISOLATE LOW ORDER DIGIT
	ANDI	REG4,7
	ADDI	REG4,"0"		; MAKE IT ASCII
	PUSH	TOPP,REG4		; SAVE IT
	LSH	REG3,-3			; WRITE NEXT ORDER CHAR FIRST, IF NOT 0
	SKIPE	REG3
	PUSHJ	TOPP,WRNUM		; RECURSION WRITES LEFTMOST DIGIT 1ST
	POP	TOPP,REG4		; WRITE RIGHT HAND DIGIT
	JRST	WRCHR

WRNAME:	MOVE	REG5,[POINT 6,REG3]	; NAME IS IN REG3
WRNLP:	ILDB	REG4,REG5		; GET NEXT SIXBIT CHAR
	JUMPE	REG4,WRRET		; DONE IF SIXBIT BLANK
	ADDI	REG4," "		; CONVERT SIXBIT TO ASCII
	PUSHJ	TOPP,WRCHR
	TRNE	REG5,1			; WHEN 6 HAVE BEEN WRITTEN, ADDRESSES REG4 (EVEN)
	JRST	WRNLP
PATHRT:
WRRET:
	POPJ	TOPP,

;
;    Error return on failure of extended lookup: just return
;    null string.
;

PATHERROR:
	SETZM	0(REG1)
	POPJ	TOPP,

	LIT
	PRGEND
